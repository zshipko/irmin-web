open Lwt.Infix
open Tyxml
open Cohttp_lwt_unix

let irmin_js = [%blob "../../js/irmin.js"]

let read_file filename =
  let ic = open_in filename in
  let len = in_channel_length ic in
  let res = really_input_string ic len in
  close_in ic; res

module Make (Store : Irmin.S)(Config : Irmin_graphql.Server.CONFIG) = struct
  module Graphql = Irmin_unix.Graphql.Server.Make (Store) (Config)

  type t =
    { store : Store.repo
    ; title: string
    ; html : string
    ; css : string
    ; js : string
    ; allow_mutations : bool }

  let create ?(allow_mutations = true) ~title ~html ~css ~js store =
    {store; title; html; css; js; allow_mutations}

  let check t doc =
    let open Graphql_parser in
    if not t.allow_mutations then
      List.for_all
        (fun op ->
          match op with
          | Operation {optype = Mutation; _} ->
            false
          | _ ->
            true )
        doc
    else true

  let irmin_js_handler address =
    Server.respond_string
      ~status:`OK
      ~headers:(Cohttp.Header.of_list [("Content-Type", "text/javacript")])
      ~body:(irmin_js
      ^ Printf.sprintf
          "\n\n//Generated by Irmin_web\nlet ir = new Irmin(\"%s\");\n" address
      ) ()

  let make_html t =
    let inner = Tyxml.Html.Unsafe.data t.html in
    let html = [%html {|
      <html>
        <head>
          <meta charset="utf-8">
          <title>|}(Html.txt t.title){|</title>
          <link rel="stylesheet" href="/static/css/main.css" />
          <script src="/irmin.js">//</script>
        </head>
        <body>
          |}[inner]{|
          <script src="/static/js/main.js">//</script>
        </body>
      </html>
    |}] in
    let buffer = Buffer.create 64 in
    let fmt = Format.formatter_of_buffer buffer in
    Tyxml.Html.pp () fmt html;
    Buffer.contents buffer

  let callback t schema address html _conn req body =
    let uri = Cohttp_lwt.Request.uri req in
    match Uri.path uri with
    | "/graphql" ->
        (Graphql.execute_request schema req body >>= function
        | `Response (res, body) -> Lwt.return (res, body)
        | `Expert _ -> failwith "Expert")
    | "/irmin.js" -> irmin_js_handler address
    | "/static/js/main.js" ->
        let headers = Cohttp.Header.of_list [("Content-Type", "application/javascript")] in
        Server.respond_string ~status:`OK ~headers ~body:t.js ()
    | "/static/css/main.css" ->
        let headers = Cohttp.Header.of_list [("Content-Type", "text/css")] in
        Server.respond_string ~status:`OK ~headers ~body:t.css ()
    | "/" ->
        let headers = Cohttp.Header.of_list [("Content-Type", "text/html")] in
        Server.respond_string ~status:`OK ~headers ~body:html ()
    | _ -> Server.respond_string ~status:`Not_found ~body:"Not found" ()

  let configure addr port = function
    | None ->
        Conduit_lwt_unix.init ~src:addr () >|= fun ctx ->
        ctx, `TCP (`Port port)
    | Some (`Certificate crt, `Key key) ->
        let tls_server_key = `TLS (`Crt_file_path crt, `Key_file_path key, `No_password) in
        Conduit_lwt_unix.init ~src:addr ~tls_server_key () >|= fun ctx ->
        ctx, `TLS (`Crt_file_path crt, `Key_file_path key, `No_password, `Port port)

  let run ?ssl ?(addr = "localhost") ?(port = 8080) t =
    let address =
      Printf.sprintf "http://%s:%d/graphql" addr port
    in
    let html = make_html t in
    let callback = callback t (Graphql.schema t.store) address html in
    configure addr port ssl >>= fun (ctx, mode) ->
    let ctx = Cohttp_lwt_unix.Net.init ~ctx () in
    let server = Server.make ~callback () in
    Server.create ~ctx ~mode server
end
